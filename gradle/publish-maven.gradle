apply plugin: 'maven-publish'

jar {
	metaInf { from "$rootDir/LICENSE" }
	metaInf { from "$rootDir/NOTICE" }
}
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
	metaInf { from "$rootDir/LICENSE" }
	metaInf { from "$rootDir/NOTICE" }
}
artifacts {
	archives sourcesJar
}
publishing {
	publications {
		maven(MavenPublication) {
			from components.java
			pom.withXml {
				asNode().dependencies.first().dependency.each {
					final artifactId = it.artifactId.text()
					if (artifactId.contains('servlet-api') || artifactId == 'javax.websocket-api') {
						it.scope.first().value = 'provided'
					} else {
						it.scope.first().value = 'compile'
					}
				}
				asNode().children().last() + {
					resolveStrategy = Closure.DELEGATE_FIRST
					organization {
						name "iSYS Software GmbH"
						url "https://www.isys-software.de/"
					}
					licenses {
						license {
							name 'The Apache Software License, Version 2.0'
							url 'http://www.apache.org/license/LICENSE-2.0.txt'
							distribution 'repo'
						}
					}
					scm {
						url "https://github.com/stagemonitor/stagemonitor"
						connection "scm:git:git://github.com/stagemonitor/stagemonitor"
						developerConnection "scm:git:git://github.com/stagemonitor/stagemonitor"
					}
					issueManagement {
						system "GitHub Issues"
						url "https://github.com/stagemonitor/stagemonitor/issues"
					}
				}
			}

			artifact sourcesJar {
				classifier "sources"
			}
		}
	}
	repositories {
		maven {
			if (project.hasProperty('mvnRepo')) {
				url mvnRepo
			}
		}
	}
}